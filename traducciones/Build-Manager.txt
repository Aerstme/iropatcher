prontera,163,191,4	script	Build Manager	862,{
function ManageBuild;

mes "[ ^2e9c99Build Manager^000000 ]";
mes "¡Hello!";
mes "Please, select a ^FF6133service^000000:";

switch( select( ( .BMMode & 1 )?"~ ^FF0000Save & change builds^000000.":"",
				( .BMMode & 2 )?"~ ^0000FFReset status^000000 (250,000z).":"",	
				( .BMMode & 4 )?"~ ^0000FFReset skills^000000 (250,000z).":"")){
	Case 1:
		if(JobLevel < 99 || BaseLevel < 255){
			mes "~ ^FF0000My services is only available for level character 255/99^000000.";
			close;
		}	
		
		for( set .@i,1; .@i <= .MaxSlots; set .@i,.@i + 1 )
			set .@Menu$,.@Menu$ + (( getd( "Slot_"+.@i+"$" ) == "" )?"^FF0000Empty Slot^000000.":getd( "Slot_"+.@i+"$" ) )+":";
		set .@Slot,select( .@Menu$ );
		do{
			next;
			mes "[ ^2e9c99Build Manager^000000 ]";
			mes "Build: ^FF0000"+(( getd( "Slot_"+.@Slot+"$" ) == "" )?"Slot "+.@Slot:getd( "Slot_"+.@Slot+"$" ) )+"^000000";
			mes "STR - ^0000FF"+getd( "STR_"+.@Slot )+"^000000      INT - ^0000FF"+getd( "INT_"+.@Slot )+"^000000";
			mes "AGI - ^0000FF"+getd( "AGI_"+.@Slot )+"^000000      DEX - ^0000FF"+getd( "DEX_"+.@Slot )+"^000000";
			mes "VIT - ^0000FF"+getd( "VIT_"+.@Slot )+"^000000      LUK - ^0000FF"+getd( "LUK_"+.@Slot )+"^000000";
			mes " ";
			mes "Remaining : ^0000FF"+getd( "STATS_"+.@Slot )+"^000000";
			set .@Option,select(( getd( "Slot_"+.@Slot+"$" ) == "" )?"":"~ ^0000FFChange build.",
								( getd( "Slot_"+.@Slot+"$" ) == "" )?"~ ^009900Save 
build^000000.":"",
								( getd( "Slot_"+.@Slot+"$" ) == "" )?"":"~ ^FF0000Wipe build^000000." );
			ManageBuild( .@Slot,.@Option);
		}while( .@Option != 1 );
		break;
	Case 2:
		if(Zeny < 250000 ){ mes "~ ^FF0000You do not have enough zeny to reset your stats^000000."; close;}
		ResetStatus;
		set Zeny, Zeny - 250000;
		mes "^009900Your stats has been reset.^000000";
		break;
	Case 3:
		if(Zeny < 250000 ){ mes "~ ^FF0000You do not have enough zeny to reset your skills^000000."; close;}
		ResetSkill;
		set Zeny, Zeny - 250000;
		mes "^009900Your skills has been reset.^000000";
		break;
}
close;

function	ManageBuild	{
switch( getarg(1) ){
	Case 1:	//	Switch Build
		message strcharinfo(0),"You have changed your build.";
			ResetStatus;
			set StatusPoint,getd( "STATS_"+getarg(0) );
			statusup2 bStr,getd( "STR_"+getarg(0) ) - 1;
			statusup2 bAgi,getd( "AGI_"+getarg(0) ) - 1;
			statusup2 bVit,getd( "VIT_"+getarg(0) ) - 1;
			statusup2 bInt,getd( "INT_"+getarg(0) ) - 1;
			statusup2 bDex,getd( "DEX_"+getarg(0) ) - 1;
			statusup2 bLuk,getd( "LUK_"+getarg(0) ) - 1;
		break;
	Case 2:	//	Save Build
			setd( "STR_"+getarg(0) ),readparam(bStr);
			setd( "AGI_"+getarg(0) ),readparam(bAgi);
			setd( "VIT_"+getarg(0) ),readparam(bVIT);
			setd( "INT_"+getarg(0) ),readparam(bInt);
			setd( "DEX_"+getarg(0) ),readparam(bDex);
			setd( "LUK_"+getarg(0) ),readparam(bLuk);
			setd( "STATS_"+getarg(0) ),StatusPoint;
		if( .Length[0] < .Length[1] && .Length[0] ){
			mes " ";
			mes "Put a ^FF0000name^000000 for your build.";
			mes "^FF0000Name^000000 : "+.Length[0]+" ~ "+.Length[1]+" Carácteres.";
			do{
				input getd( "Slot_"+getarg(0)+"$" );
			}while( getstrlen( getd( "Slot_"+getarg(0)+"$" ) ) < .Length[0] || getstrlen( getd( "Slot_"+getarg(0)+"$" ) ) > .Length[1] );
		}else{
			setd( "Slot_"+getarg(0)+"$" ),"Build "+getarg(0);
		}
		message strcharinfo(0),"Has guardado la build.";
		break;
	Case 3:	//	Remove Build
		message strcharinfo(0),"Has limpiado la build.";
			setd( "STR_"+getarg(0) ),0;
			setd( "AGI_"+getarg(0) ),0;
			setd( "VIT_"+getarg(0) ),0;
			setd( "INT_"+getarg(0) ),0;
			setd( "DEX_"+getarg(0) ),0;
			setd( "LUK_"+getarg(0) ),0;
			setd( "STATS_"+getarg(0) ),0;
			setd( "Slot_"+getarg(0)+"$" ),"";
		break;
}
return;
}	
	
OnInit:
waitingroom "[ Build Manager ]",0;
// Mode of NPC
// 1 = Save & Switch 
// 2 = Reset Status
// 4 = Reset Skills
// 7 = All of Above [ 1+2+4 = 7 ]
set .BMMode,7;
// How Many Slots available to Save Status Builds.
set .MaxSlots,5;
// Enable Stats Slot Rename + Max Name Length ( 0 = Disable )
setarray .Length,4,26; // Min. ~ Max. Words.
end;

}

// Duplicates
//============================================================
payon,184,104,4	duplicate(Build Manager)	Build Manager#pay	862
rachel,124,106,5	duplicate(Build Manager)	Build Manager#rac	862
