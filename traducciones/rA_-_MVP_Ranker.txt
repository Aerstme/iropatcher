/* log_athena.conf

// Log MVP Monster Drops (Note 1)
// Outdated. Use Pick_Log instead. But this log could be useful to keep track slayed MVPs
log_mvpdrop: yes

*/


prontera,139,183,7	script	MvP Rank	863,{
	.@cid = getcharid(0);
	query_sql("SELECT COUNT(`kill_char_id`) FROM `"+.database_schema$[0]+"`.`mvplog`",.@t1);
	while (1) {
		switch(select(
			"~ ^009900My Stats^000000.",
			"~ Global Ranking (Top "+.top_rank_global+").",
			"~ Monthly Ranking (Top "+.top_rank_monthly+").",
			"~ Weekly Rangking (Top "+.top_rank_weekly+").",
			"~ Filter by MvP ^ff0000["+.@t1+"]^000000.",
			"~ ^1E90FFInformation and prizes^000000.",
			"Salir."
		)) {
			case 1:
				mes "[ ^2e9c99My Stats^000000 ]";
				query_sql("SELECT COUNT(`mvp_id`) FROM `"+.database_schema$[0]+"`.`mvplog` WHERE `kill_char_id` = "+.@cid+" "+.disabled_map_query$+" GROUP BY `kill_char_id`", .@total_kill);
				mes "Total MvP: "+"^ff0000["+F_InsertComma(.@total_kill)+"]^000000";
				query_sql("SELECT `monster_id` FROM `"+.database_schema$[0]+"`.`mvplog` WHERE `kill_char_id` = "+.@cid+" "+.disabled_map_query$+" ORDER BY `mvp_id` DESC LIMIT 1", .@last_mob_id);
				mes "Last MvP: "+getmonsterinfo(.@last_mob_id, MOB_NAME);
				mes " ";
				mes "MvPs Record:";
				.@size = query_sql("SELECT `monster_id`, COUNT(`mvp_id`) AS `total` FROM `"+.database_schema$[0]+"`.`mvplog` WHERE YEAR(`mvp_date`) = "+.year+" AND `kill_char_id` = "+.@cid+" "+.disabled_map_query$+" GROUP BY `kill_char_id`,`monster_id` ORDER BY `total` DESC", .@mob_id, .@mob_kill);
				if (.@size) {
					for (.@i = 0; .@i < .@size; .@i++)
						mes getmonsterinfo(.@mob_id[.@i], MOB_NAME)+" - " + "^ff0000["+.@mob_kill[.@i]+"]^000000";
				}
				else {
					mes "~ ^FF0000No Records^000000.";
				}
				break;
			case 2:
				mes "[ ^2e9c99Top "+.top_rank_global+" Global^000000 ]";
				.@sql$ = "SELECT c.`name`, COUNT(`kill_char_id`) AS `total` FROM `"+.database_schema$[0]+"`.`mvplog` rank INNER JOIN `"+.database_schema$[1]+"`.`char` c ON c.`char_id` = rank.`kill_char_id` WHERE YEAR(rank.`mvp_date`) = "+.year+" "+.disabled_map_query$+" GROUP BY rank.`kill_char_id` ORDER BY `total` DESC LIMIT "+.top_rank_global;
				.@size = query_sql(.@sql$, .@name$, .@total);
				if (.@size) {
					for (.@i = 0; .@i < .@size; .@i++)
						mes "Top "+(.@i+1)+": ^0055FF"+.@name$[.@i]+"^000000 - ^FF0000"+.@total[.@i]+"^000000 MvP(s).";
				}
				else {
					mes "~ ^FF0000No records^000000.";
				}
				break;
			case 3:
				.@month = select(
					"~ Current Month.",
					"January.",
					"February.",
					"March.",
					"April.",
					"May.",
					"June.",
					"July.",
					"August.",
					"September.",
					"October.",
					"November.",
					"Dicember."
				) - 1;
				if (.@month <= 0)
					.@month = .month;
				mes "[ ^2e9c99Top "+.top_rank_monthly+" Mensual - Mes #"+.@month+"^000000 ]";
				.@sql$ = "SELECT c.`name`, COUNT(`kill_char_id`) AS `total` FROM `"+.database_schema$[0]+"`.`mvplog` rank INNER JOIN `"+.database_schema$[1]+"`.`char` c ON c.`char_id` = rank.`kill_char_id` WHERE YEAR(rank.`mvp_date`) = "+.year+" AND MONTH(rank.`mvp_date`) = "+.@month+" "+.disabled_map_query$+" GROUP BY rank.`kill_char_id` ORDER BY `total` DESC LIMIT "+.top_rank_monthly;
				.@size = query_sql(.@sql$, .@name$, .@total);
				if (.@size) {
					for (.@i = 0; .@i < .@size; .@i++)
						mes "Top "+(.@i+1)+": ^0055FF"+.@name$[.@i]+"^000000 - ^FF0000"+.@total[.@i]+"^000000 MvP(s).";
				}
				else {
					mes "~ ^FF0000No records^000000.";
				}
				break;
			case 4:
				mes "[ ^2e9c99Top "+.top_rank_weekly+" Semanal - Semana #"+.week+"^000000 ]";
				.@sql$ = "SELECT c.`name`, COUNT(`kill_char_id`) AS `total` FROM `"+.database_schema$[0]+"`.`mvplog` rank INNER JOIN `"+.database_schema$[1]+"`.`char` c ON c.`char_id` = rank.`kill_char_id` WHERE YEAR(rank.`mvp_date`) = "+.year+" AND WEEKOFYEAR(rank.`mvp_date`) = "+.week+" "+.disabled_map_query$+" GROUP BY rank.`kill_char_id` ORDER BY `total` DESC LIMIT "+.top_rank_weekly;
				.@size = query_sql(.@sql$, .@name$, .@total);
				.@size = query_sql(.@sql$, .@name$, .@total);
				if (.@size) {
					for (.@i = 0; .@i < .@size; .@i++)
						mes "Top "+(.@i+1)+": ^0055FF"+.@name$[.@i]+"^000000 - ^FF0000"+.@total[.@i]+"^000000 MvP(s).";
				}
				else {
					mes "~ ^FF0000No records^000000.";
				}
				break;
			case 5:
				.@mvp_menu$ = "";
				freeloop(true);
				for (.@i = 0; .@i < .mvp_size; .@i++) {
					query_sql("SELECT COUNT(`kill_char_id`) FROM `"+.database_schema$[0]+"`.`mvplog` rank WHERE YEAR(rank.`mvp_date`) = "+.year+" AND `monster_id` = "+.mvp_list[.@i],.@t2);
					.@mvp_menu$ = .@mvp_menu$ + getmonsterinfo(.mvp_list[.@i], MOB_NAME) + " ^ff0000["+.@t2+"]^000000.";
					.@mvp_menu$ += ":";
				}
				freeloop(false);
				.@mvp = select(.@mvp_menu$) - 1;
				mes "[ ^2e9c99MVP - "+getmonsterinfo(.mvp_list[.@mvp], MOB_NAME)+"^000000 ]";
				.@sql$ = "SELECT c.`name`, COUNT(`kill_char_id`) AS `total` FROM `"+.database_schema$[0]+"`.`mvplog` rank INNER JOIN `"+.database_schema$[1]+"`.`char` c ON c.`char_id` = rank.`kill_char_id` WHERE YEAR(rank.`mvp_date`) = "+.year+" AND `monster_id` = "+.mvp_list[.@mvp]+" "+.disabled_map_query$+" GROUP BY rank.`kill_char_id` ORDER BY `total` DESC LIMIT "+.top_rank_global;
				.@size = query_sql(.@sql$, .@name$, .@total);
				if (.@size) {
					for (.@i = 0; .@i < .@size; .@i++)
						mes "Top "+(.@i+1)+": ^0055FF"+.@name$[.@i]+"^000000 - ^FF0000["+.@total[.@i]+"]^000000.";
				}
				else {
					mes "~ ^FF0000No records^000000.";
				}
				break;
			case 6:
				mes "[ ^2e9c99Information^000000 ]";
				mes "Hello!";
				mes "With me you can see the records of all players that kill ^FF6133MVP(s)^000000 in ^2e9c99Iridium RO^000000.";
				next;
				mes "[ ^2e9c99Information^000000 ]"; 
				mes "Its my duty show to the players the global, monthly and weekly statistics.";
				next;	
				mes "[ ^2e9c99Prizes^000000 ]";
				mes "The prizes will be given ^800080monthly^000000 to the TOP 3 players ranking.";
				mes "~ ^FF0000Note: The Ranking does not apply restriction on any map^000000.";
				next;
				mes "[ ^2e9c99Prizes^000000 ]";
				mes "======== Prizes ========";
				mes "^009900First place^000000: 45,000,000 Zeny + i dont know";
				mes " ";
				mes "^00BFFFSecond place^000000: 30,000,000 Zeny + i dont know";
				mes " ";
				mes "^FF33E6Third Place^000000: 15,000,000 Zeny + i dont know";
				next;
				mes "[ ^2e9c99Prizes^000000 ]";
				mes "End of the ^800080month^000000, The winners will receive their prizes via RoDEX (Mail).";
				mes "-----------------------------------";
				mes "~ ^777777Cheer up! and go kill MvP(s)!";
				break;
			default:
				close;
		}
		next;
	}
	close;
	
	OnInit:
		waitingroom "[ MvP Rank ]",0;
		
		// database schema name
		setarray .database_schema$, "rathena_log", "rathena_server";
		
		// top kill rank are listed.
		.top_rank_global = 25;
		.top_rank_monthly = 25;
		.top_rank_weekly = 25;
		
		// top 3 every month are rewarded.
		.top_rank_reward = 3;
		
		// disabled maps
		// setarray .disabled_map$, "06guild_%", "%cas_g%", "%@%", "prontera";
		.disabled_map_size = getarraysize(.disabled_map$);
		for (.@i = 0; .@i < .disabled_map_size; .@i++)
			.disabled_map_query$ += "AND `map` NOT LIKE '"+escape_sql(.disabled_map$[.@i])+"' ";
			
		.mvp_size = query_sql("SELECT `id` FROM `"+.database_schema$[1]+"`.`mob_db` WHERE `mexp` > 0 ORDER BY `kName`", .mvp_list);
		
	OnClock0000:
		// assign rodex reward at 1st of every month.
		if (.day == 1 && .month > 0) {
			.@sql$ = "SELECT rank.`kill_char_id`, COUNT(`kill_char_id`) AS `total` FROM `"+.database_schema$[0]+"`.`mvplog` rank INNER JOIN `"+.database_schema$[1]+"`.`char` c ON c.`char_id` = rank.`kill_char_id` WHERE YEAR(rank.`mvp_date`) = "+.year+" AND MONTH(rank.`mvp_date`) = "+.month+" "+.disabled_map_query$+" GROUP BY rank.`kill_char_id` ORDER BY `total` DESC LIMIT "+.top_rank_reward;
			.@size = query_sql(.@sql$, .@cid, .@total);
			
			for (.@i = 0; .@i < .@size; .@i++) {
				.@rank = .@i+1;
				switch (.@rank) {
					case 1: // Top Rank 1 reward
						.@zeny = 5000000;
						setarray .@itemid, 501, 502, 503;
						setarray .@amount, 100, 100, 100;
						debugmes ""+.@rank;
						break;
					case 2: // Top Rank 2 reward
						.@zeny = 500000;
						setarray .@itemid, 501, 502, 503;
						setarray .@amount, 10, 10, 10;
						debugmes ""+.@rank;
						break;
					case 3: // Top Rank 3 reward
						.@zeny = 5000;
						setarray .@itemid, 501, 502, 503;
						setarray .@amount, 1, 1, 1;
						debugmes ""+.@rank+" - "+.@cid[.@i];
						break;
					default: continue;
				}
				mail .@cid[.@i], "SYSTEM", "**TOP #"+.@rank+" MVP RANK (MONTHLY) **", "Congratulation! You are rewarded for being Top #"+.@rank+" in the Monthly MVP Rank.", .@zeny, .@itemid, .@amount;
			}
		}
		// update date info
		query_sql("SELECT DAYOFMONTH(NOW()), MONTH(NOW()), YEAR(NOW()), WEEKOFYEAR(NOW())", .day, .month, .year, .week);
		end;
}
