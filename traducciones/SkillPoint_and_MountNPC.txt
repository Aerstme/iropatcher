
prtbar,44,16,2	script	Skill Points	897,{
	.@skpoint = 21;
	if ( BaseLevel < 255 || JobLevel < 99 ) {
		mes "[ ^2e9c99Skill Points^000000 ]";
		mes "~ ^FF0000My service is only available for level character 255/99^000000.";
		close;
	}
	if ( SKPointService ) {
		mes "[ ^2e9c99Skill Points^000000 ]";
		mes "~ ^FF0000You only can use my service once for character^000000.";
		close;
	}
	setarray .@item,
		512,2, // Apple x 2
		513,3; // Banana x 2
	mes "[ ^2e9c99Skill Points^000000 ]";
	mes "¡Hello!";
	mes "With me you can get "+.@skpoint+" ^800080Skill Points^000000 to be able to perform your work better in PvE y PvP.";
	next;
	mes "[ ^2e9c99Skill Points^000000 ]";
	mes "For that, i will need you to bring me the next ^1E90FFitems^000000:";
	for (.@i=0;.@i<getarraysize(.@item);.@i+=2)
		mes "- " + .@item[.@i+1] + "x " + "^FF6133" + getitemname(.@item[.@i]) + "^000000.";
	mes "-----------------------------------";
	mes "Do you have everything ready?";
	next;
	if(select("~ ^009900Give items^000000.:~ ^FF0000No, not yet^000000.")==2)close;
	for (.@i=0;.@i<getarraysize(.@item);.@i+=2)
		if ( countitem(.@item[.@i]) < .@item[.@i+1]) .@error = true;
	if ( .@error ) {
		mes "[ ^2e9c99Skill Points^000000 ]";		
		mes "~ ^FF0000You dont have what i need to enjoy my services^000000.";
		close;
	}
	for (.@i=0;.@i<getarraysize(.@item);.@i+=2)
		delitem .@item[.@i], .@item[.@i+1];
	mes "[ ^2e9c99Skill Points^000000 ]";
	mes "^009900¡Ready!^000000";
	mes "Distribute your points wisely";
	close2;
	SkillPoint += .@skpoint;
	SKPointService = true;
	end;

OnInit:

	waitingroom "[ Skill Points ]",0;
end;

}


prontera,147,155,6	script	Mounts	727,{
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "¡Hello!";
	mes "With you can get your ^800080mounts^000000 unlocking the command ^1E90FF@ride^000000 in your account.";
	next;
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "The ^800080mounts^000000 Only serve to walk a little faster, you get the same speed as if you were using a Box of Thunder, so it does give any advantage.";
	next;
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "Each job have his own ^800080mount^000000, in which you get a very nice and special touch, in addition to saving a lot of time and zeny, farming or buying Box of Thunder's.";
	next;
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "~ ^FF0000You cant use the mounts in Events, WoE or BattleGround^000000.";
	mes "The cost to unlock ^800080mounts^000000 in your account is 20,000 ^FF6133Cash points^000000.";
	next;
	menu "~ ^777777View Mounts^000000.", ViewMount, "~ ^009900Unlock Mount.", UnlockMount;
	
ViewMount:
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "Click on the link that will appear in the next window to see the ^800080mounts^000000 and their ^FF8C00animations^000000.";
	next;
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "~ ^777777Link^000000: <URL>View mounts<INFO>http://thor.iridium-ro.com/monturas/,800,800</INFO></URL>";
	close;

UnlockMount:
	if ( !#MountPaid ) {
	mes "[ ^2e9c99Monturas^000000 ]";
	mes "I will need you give me 20,000 ^FF6133Cash points^000000 To unlock the free use of ^800080mounts^000000 in your account.";
	mes "Actual state: "+ #CASHPOINTS + " ^FF6133Cash points^000000.";
	mes "Do you have them ready?";
	next;
	if (select("~ ^009900Give Cash points^000000.:~ ^FF0000No, not yet^000000.")==2)close;
	if ( #CASHPOINTS < 20000 ) {
		mes "[ ^00BFFFMounts^000000^000000 ]";
		mes "~ ^FF0000You dont have what i need to enjoy my services^000000.";
		close;
	}
	#CASHPOINTS -= 20000;
	#MountPaid = true;
	}
	mes "[ ^2e9c99Mounts^000000 ]";
	mes "^009900¡Ready!^000000";
	mes "Now you can use the command ^1E90FF@ride^000000 with all the characters in your account.";
	close;
	
OnPCStatCalcEvent: // Should be removed on WoE and Battleground
	if ( !#MountPaid ) end;
	if ( getmapflag(strcharinfo(3),mf_gvg) || getcharid(4) ) if ( ismounting() ) setmounting();
	end;

OnMount:
	if ( !#MountPaid ) {
		dispbottom "#IRO: Necesitas pagar 20,000 Cash points al NPC para disfrutar de su servicio.", 0xffff00;
		end;
	}
	if ( getmapflag(strcharinfo(3),mf_gvg) || getcharid(4) ) {
		dispbottom "#IRO: You cant use the mounts in Events, WoE or BG.", 0xffff00;
		end;
	}
	if ( ismounting() ) setmounting();
	else setmounting();
	end;
	
OnInit:
	bindatcmd "ride", strnpcinfo(0)+"::OnMount";
	waitingroom "[ Mounts ]",0;
end;

}


